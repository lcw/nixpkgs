{
  lib,
  buildPythonPackage,
  fetchPypi,
  fetchFromGitHub,
  cython,
  fastrlock,
  numpy,
  pytestCheckHook,
  mock,
  setuptools,
  cudaPackages,
  autoAddDriverRunpath,
  pythonOlder,
}:
let
  # NOTE: Upstream still isn't compatible with Cython 3.0 as of 13.1.0:
  # https://github.com/cupy/cupy/issues/4610
  cython_2 = cython.overridePythonAttrs (
    prevAttrs:
    let
      version = "0.29.36";
      hash = "sha256-QcDP0tdU44PJ7rle/8mqSrhH0Ml0cHfd18Dctow7wB8=";
    in
    {
      inherit version;
      src = fetchPypi {
        pname = "Cython";
        inherit version hash;
      };
    }
  );

  # General CUDA dependencies
  inherit (cudaPackages)
    cuda_cudart
    cuda_nvcc
    cuda_nvrtc
    cuda_nvtx
    cuda_profiler_api
    libcublas
    libcufft
    libcurand
    libcusolver
    libcusparse
    nccl
    ;

  # Specific CUDA dependencies
  cudnn = cudaPackages.cudnn_8_8;
  libcutensor = cudaPackages.libcutensor_2_0;
  libcusparse_lt = cudaPackages.libcusparse_lt_0_6;
in
# https://docs.cupy.dev/en/v13.1.0/install.html
assert lib.assertMsg (cudaPackages ? cudnn_8_8) "cupy requires cudnn 8.8";
assert lib.assertMsg (cudaPackages ? libcutensor_2_0) "cupy requires cutensor 2.0";
# NOTE: We apply a patch so we can use 0.6 instead of 0.2.
assert lib.assertMsg (cudaPackages ? libcusparse_lt_0_6) "cupy requires cusparseLt 0.6";
buildPythonPackage rec {
  pname = "cupy";
  version = "13.1.0";
  pyproject = true;

  build-system = [
    cython_2
    fastrlock
    setuptools
  ];

  disabled = pythonOlder "3.9";

  src = fetchFromGitHub {
    owner = pname;
    repo = pname;
    rev = "v${version}";
    hash = "sha256-9sx4wBX/b1XPdF9ALQThVyVfoVGX19HteiZSM/suyI0=";
    fetchSubmodules = true;
  };

  patches = [
    # Vendored from https://github.com/cupy/cupy/pull/8074 on 2024-05-30
    # Created by checking out the v13.1.0 tag and manually cherry-picking all the commits which weren't
    # the author merging main into the branch. Then generated by `git diff -p tags/v13.1.0`.
    # NOTE: The PR doesn't apply cleanly to 13.1.0, so it was necessary to manually cherry-pick the commits.
    ./patch-for-cusparseLt-0.6.1.patch
  ];

  # See https://docs.cupy.dev/en/v10.2.0/reference/environment.html. Seting both
  # CUPY_NUM_BUILD_JOBS and CUPY_NUM_NVCC_THREADS to NIX_BUILD_CORES results in
  # a small amount of thrashing but it turns out there are a large number of
  # very short builds and a few extremely long ones, so setting both ends up
  # working nicely in practice.
  preConfigure = ''
    export CUPY_NUM_BUILD_JOBS="$NIX_BUILD_CORES"
    export CUPY_NUM_NVCC_THREADS="$NIX_BUILD_CORES"
  '';

  nativeBuildInputs = [
    autoAddDriverRunpath
    cuda_nvcc
  ];

  buildInputs = [
    cuda_cudart # <cuda_runtime_api.h> -lcudart
    cuda_nvrtc # <nvrtc.h> # -lnvrtc
    cuda_nvtx.dev # <nvtx3/nvToolsExt.h>
    cuda_profiler_api.dev # <cuda_profiler_api.h>
    # TODO(@connorbaker): If we don't explicitly use the dev output of cudnn, CuPy can't find the header.
    cudnn.dev # <cudnn.h>
    cudnn.lib # -lcudnn
    libcublas # <cublas_v2.h> -lcublas
    libcufft # <cufft.h> -lcufft
    libcurand # <curand.h> -lcurand
    libcusolver # <cusolverDn.h> -lcusolver
    libcusparse_lt # <cusparseLt.h> -lcusparseLt
    libcusparse # <cusparse.h> -lcusparse
    libcutensor # <cutensor.h> -lcutensor
    nccl # <nccl.h> -lnccl
  ];

  dependencies = [
    fastrlock
    numpy
  ];

  nativeCheckInputs = [
    pytestCheckHook
    mock
  ];

  # Won't work with the GPU, whose drivers won't be accessible from the build
  # sandbox
  doCheck = false;

  enableParallelBuilding = true;

  meta = with lib; {
    description = "NumPy-compatible matrix library accelerated by CUDA";
    homepage = "https://cupy.chainer.org/";
    changelog = "https://github.com/cupy/cupy/releases/tag/v${version}";
    license = licenses.mit;
    platforms = [ "x86_64-linux" ];
    maintainers = with maintainers; [ hyphon81 ];
  };
}
